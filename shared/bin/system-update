#!/usr/bin/env fish

function warn
  set_color yellow
  echo $argv
  set_color normal
end

set -l homelab_root (realpath (status dirname)/../..)

set -l os_name (uname -o)
switch $os_name
case "GNU/Linux"
  set os_name "linux"
case "Darwin"
  set os_name "macos"
end

if test "$os_name" = "linux"; and type -q apt
  warn "Running apt update..."
  sudo apt update
  warn "Running apt full-upgrade..."
  sudo apt full-upgrade -y
  warn "Running apt autoremove..."
  sudo apt autoremove -y
end

warn "Running brew update..."
brew update -f
warn "Running brew upgrade..."
brew upgrade

warn "pip install --upgrade pip..."
pip install --upgrade pip
warn "pip install -U python-shellrunner..."
pip install -U python-shellrunner
set -l pth_file "$(python -c "import sysconfig; print(sysconfig.get_path('purelib'))")/homelab_lib.pth"
echo "$homelab_root/lib/" | sudo tee "$pth_file" >/dev/null
warn "Added homelab/lib/ to python search path."

if type -q nvm
  warn "Running nvm install latest..."
  nvm install latest
  warn "Running npm install -g npm..."
  npm install -g npm
  warn "Running npm update -g..."
  npm update -g
end

if type -q pnpm
  warn "Running pnpm add -g pnpm..."
  pnpm add -g pnpm
  warn "Running pnpm install -g..."
  pnpm install -g
  warn "Running pnpm update -g..."
  pnpm update -g
end

warn "Running fisher update..."
fisher update

if type -q sdk
  warn "Running sdk selfupdate..."
  sdk selfupdate
  warn "Running sdk update..."
  sdk update
  curl -sLo ~/.config/fish/functions/fenv.main.fish https://raw.githubusercontent.com/oh-my-fish/plugin-foreign-env/master/functions/fenv.main.fish
  curl -sLo ~/.config/fish/functions/fenv.fish https://raw.githubusercontent.com/oh-my-fish/plugin-foreign-env/master/functions/fenv.fish
  curl -sLo ~/.config/fish/completions/sdk.fish https://raw.githubusercontent.com/deather/omf-sdk/master/completions/sdk.fish
  curl -sLo ~/.config/fish/functions/sdk.fish https://raw.githubusercontent.com/deather/omf-sdk/master/functions/sdk.fish
end

if test "$hostname" = "sid"
  ~/restic/restic self-update
  mergerfs-update.py
  snapraid-btrfs-runner-update.py
  snapraid-btrfs-update.py
  snapraid-update.py
  docker-container-update.py
end

if test "$hostname" = "adguard"
  ~/AdGuardHome/AdGuardHome --update
end

warn "System updated. Make sure to reboot."
